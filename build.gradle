//https://github.com/jfrog/project-examples/blob/master/gradle-examples/gradle-example-minimal/build.gradle#L53
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.+')
    }
}

plugins {
    id 'com.github.johnrengelman.shadow'
    id 'java'
    id 'maven-publish'
}

group = 'fr.skytale'
version = '5.3.3'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
allprojects {
    repositories {
        maven { url 'https://repository.skytale.fr/artifactory/public' }
    }
}
repositories {
    maven { url 'https://jitpack.io' }
    mavenCentral()

    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven { url 'https://oss.sonatype.org/content/groups/private/' }
}

dependencies {
    implementation 'org.jetbrains:annotations:24.0.1'
    // The Spigot API with no shadowing. Requires the OSS repo.
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    implementation 'xyz.xenondevs:particle:1.8.4'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'com.github.mokiat:java-data-front:2.0.0'
    implementation 'com.github.ben-manes.caffeine:caffeine:2.9.3'
    implementation project(':ttfparser')
}

tasks.register("updateReadmeVersion") {
    description = "Updates the version number into the README.md file."
    doFirst {
        // Maven
        ant.replaceregexp(match: '<version>([0-9\\.]+)</version>', replace: "<version>${version}</version>", flags: 'g', byline: true) {
            fileset(dir: '.', includes: 'README.md')
        }
        // Gradle
        ant.replaceregexp(match: 'fr\\.skytale\\:particle-animation-lib\\:([0-9\\.]+)', replace: "fr.skytale:particle-animation-lib:${version}", flags: 'g', byline: true) {
            fileset(dir: '.', includes: 'README.md')
        }
        println("Updated version number in README.md")
    }
}
compileJava {
    dependsOn updateReadmeVersion
}

java {
    withSourcesJar()
    withJavadocJar()
}

task myJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

tasks.withType(ProcessResources) {
    duplicatesStrategy = DuplicatesStrategy.WARN
}

tasks.withType(AbstractCopyTask) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

sourceSets.main.resources {
    srcDirs "src/main/resources"
}

// Rename generated jar
shadowJar {
    archiveBaseName.set('particleanimlib')
    archiveClassifier.set('')
}
//
//apply plugin: 'com.jfrog.artifactory'
//apply plugin: 'java'
//apply plugin: 'maven-publish'
//
//publishing {
//    publications {
//        mavenJava(MavenPublication) {
//            from components.java
//        }
//    }
//}

if (project.hasProperty('artifactory_user')) {
    apply plugin: 'com.github.johnrengelman.shadow'
    publishing {
        publications {
            shadow(MavenPublication) { publication ->
                project.shadow.component(publication)
                artifacts = [ shadowJar, javadocJar, sourcesJar ]
                pom {
                    name.set("Particle animation lib")
                    description.set("Minecraft particle animation library")
                    url.set("https://gitlab.com/skytale_/skytale-mc/libraries/particleanimlib")
                    licenses {
                        license {
                            name.set("GNU GENERAL PUBLIC LICENSE, Version 3")
                            url.set("https://www.gnu.org/licenses/gpl-3.0.html")
                        }
                    }
                    developers {
                        developer {
                            id.set("bZx")
                            name.set("Benjamin Boulaud")
                            email.set("ben.nbld@gmail.com")
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                name = "Skytale"
                url = "https://repository.skytale.fr/artifactory/public"
                credentials {
                    username = "${artifactory_user}"
                    password = "${artifactory_password}"
                }
            }
        }
    }
}
